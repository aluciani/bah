#include "<stdlib.h>"
#include "<stdio.h>"
#include "<dirent.h>"


#include "cBinds.bah"
#include "memory.bah"
#include "string.bah"

#define! FILE ptr


//Returns the text inputed by the user inside the terminal.
stdinput() cpstring {
    buffer cpstring = memoryAlloc(1025)
    response = string("")
    nDataLength = read(<ptr>0,buffer,1024)
    if nDataLength < 1024 {
        response.append(buffer)
    } else {
        for nDataLength > 0 {
            response.append(buffer)
            if nDataLength < 1024 {
                trimAmm = 1024 - nDataLength
                response.trimRight(trimAmm)
                break
            } else {
                nDataLength = read(<ptr>0,buffer,1024)
            }
        }
    }
    response.trimRight(1)
    return response.str()
}

//Prints text to the terminal and adds a line return at the end.
println(s cpstring) {
    write(<ptr>1, s+"\n", strlen(s)+1)
    // write(<ptr>1, "\n", 1)
}

#define! fwrite(w ptr, s int, f int, hndl FILE*)
#define! fread(w ptr, s int, f int, hndl FILE*) int
#define! ftell(hndl FILE*) int
#define! fseek(hndl FILE*, offset int, from int)
#define! fputc(c char, hndl FILE*)

//A wrapper that makes file manipulation easier.
//*Note: after openning a file, you will need to close it.*
//**Warning:** fileStream can be many, many times slower than fileMap.
//So it is recommended to **use fileMap as much as possible**.
struct fileStream {
    handle: FILE*

    //Checks if the current opened file is valid or not.
    isValid() int {
        if this.handle == null {
            return 0
        }
        return 1
    }

    //Opens a file by its name.
    //The differents modes can be found [here](https://koor.fr/C/cstdio/fopen.wp).
    open(path cpstring, mode cpstring) {
        this.handle = fopen(path, mode)
    }

    //Closes an opennend connection to a file.
    close() {
        if this.isValid() == 0 {
            return
        }
        fclose(this.handle)
    }

    //Returns the position of the file reader.
    getPos() int {
        if this.isValid() == 0 {
            return 0
        }
        return ftell(this.handle)
    }

    //Set the position of the file reader.
    setPos(i int) {
        if this.isValid() == 0 {
            return
        }
        fseek(this.handle, i, 0)
    }

    //Returns the size of a file in bytes.
    getSize() int {
        if this.isValid() == 0 {
            return -1
        }
        oldPos = this.getPos()
        fseek(this.handle, 0, 2)
        size int = ftell(this.handle)
        this.setPos(oldPos)
        return size
    }

    //Sets the reading cursor to the beginning of the file.
    rewind() {
        rewind(this.handle)
    }

    //Returns a char at the current reading cursor in the file and moves the reading cursor to the next char.
    getChar() char {
        c = getc(this.handle)
        return c
    }

    //Adds a character to the file.
    setChar(c char) {
        fputc(c, this.handle)
    }

    //Creates a file by name.
    createFile(path cpstring) {
        this.open(path, "w")
        this.close()
    }

    //Writes the opened file with the specified content.
    writeFile(content cpstring) int {
        if this.isValid() == 0 {
            return -1
        }

        fputs(content, this.handle)

        return 1
    }

    //Serialize a pointer and write it to a file.
    //a: the pointer to serialize
    //s: sizeof() the pointer's content
    writePtr(a ptr, s int) {
        fwrite(a, s, 1, this.handle)
    }

    //Read a file and unserialize its content to a pointer.
    //a: the pointer to unserialize
    //s: sizeof() the pointer's content.
    readPtr(a ptr, s int) int {
        return fread(a, s, 1, this.handle)
    }

    //Returns the content of the opened file.
    readContent() cpstring {
        if this.isValid() == 0 {
            return "invalid"
        }

        sz = this.getSize()
        r cpstring = memoryAlloc(sz + 1)
        l = fread(r, 1, sz, this.handle)
        if sz != l {
            rarr = strAsArr(r)
            rarr[l] = <char>0
        }
        return r
    }

    //Returns the file's content as an array of bytes (char).
    readBytes() []char {
        r = []char
        allocateArray(r, this.getSize())

        this.readPtr(noCheck(r->data), len(r))

        return r
    }

    //Write an array of bytes (char) to a file.
    writeBytes(d []char) {
        this.writePtr(noCheck(d->data), len(d))
    }

}

struct! stat {
    st_size: int
    st_mtime: int
    st_ctime: int
}
#define! open(fn cpstring, flags int, mode int) int
#define! close(fd int) int
#define! fstat(fd int, fs stat*)
#define! stat(path cpstring, s stat*)
#linux {
    #define! munmap(p ptr, l int)
}
#darwin {
    #define! munmap(p ptr, l int)
}

//A wrapper to make file direct operation, wihtout needing to write file on changes.
//**This is way faster than the fileStream** but every modification made to the returned string will be directly made to the file.
struct fileMap {
    handle: int = -1
    size:int
    p: ptr

    //To open a file inside the fileMap.
    //It returns the content of the file as cpstring.
    open(fileName cpstring) cpstring {
        #linux {
            this.handle = open(fileName, noCheck(O_RDWR), noCheck(S_IRUSR | S_IWUSR))
            sb = stat{}
            fstat(this.handle, &sb)
            this.size = sb.st_size
            this.p = mmap(0, sb.st_size, noCheck(PROT_READ | PROT_WRITE), noCheck(MAP_SHARED), this.handle, 0)
            return this.p
        }
        #darwin {
            this.handle = open(fileName, noCheck(O_RDWR), noCheck(S_IRUSR | S_IWUSR))
            sb = stat{}
            fstat(this.handle, &sb)
            this.size = sb.st_size
            this.p = mmap(0, sb.st_size, noCheck(PROT_READ | PROT_WRITE), noCheck(MAP_SHARED), this.handle, 0)
            return this.p
        }

        #windows {
            fs = fileStream{}
            fs.open(fileName, "r+")
            iv = fs.isValid()
            if iv == 1 {
                this.handle = 0
            } else {
                this.handle = -1
            }
            file = fs.readContent()
            fs.close()
            //file cpstring = mmap(0, sb.st_size, noCheck(PROT_READ | PROT_WRITE), noCheck(MAP_SHARED), this.handle, 0)
            return file
        }

    }

    //Check if file is valid or not.
    isValid() int {
        if this.handle < 0 {
            return 0
        }
        return 1
    }

    //To close a file after you are done working on it.
    close() {
        #linux {
            if this.handle == -1 {
                return
            }
            munmap(this.p, this.size)
            close(this.handle)
        }
        #darwin {
            if this.handle == -1 {
                return
            }
            munmap(this.p, this.size)
            close(this.handle)
        }
    }
}

#define! DIR int

struct! dirent {
    d_name: cpstring
}

#define! opendir(path cpstring) DIR*
#define! closedir(d DIR*)
#define! readdir(d DIR*) dirent*

//Returns a list of all files found inside the specified directory.
listFiles(dir cpstring) []cpstring {
    files = []cpstring
    d = opendir(dir)
    file = readdir(d)
    for file != null {
        lf = len(files)
        if strcmp(file.d_name, ".") == 0 {
            file = readdir(d)
            continue
        }
        if strcmp(file.d_name, "..") == 0 {
            file = readdir(d)
            continue
        }
        files[lf] = __STR(file.d_name)
        file = readdir(d)
    }
    closedir(d)
    return files
}

#define! access(fn cpstring, m int) int

//Checks if a file exists.
fileExists(path cpstring) int {
    is = access(path, 0)
    if is == 0 {
        return 1
    }
    return 0
}

#define! remove(fn cpstring) int

//Removes a file. 'name' should be the file name, returns true if the file was deleted.
removeFile(name cpstring) bool {
    if remove(name) == 0 {
        return true
    }
    return false
}

//Get the timestamp of the last modification of a file.
getLastModified(name cpstring) int {
    attr stat
    stat(name, &attr)
    return attr.st_ctime
}

#define! getenv(s cpstring) cpstring
#define! mkdir(s cpstring, m int) int
#define! rename(o cpstring, n cpstring)


#windows {
    __windows_mkdir(s cpstring, m int) int {
        return noCheck(mkdir(s))
    }
    const mkdir = __windows_mkdir
}

//Example
// #include "iostream.bah"
// main() {
// fs = fileStream{}
// fs.open("./myFile.txt", "r")
// fileContent = fs.readContent()
// println(fileContent)
// fs.close()
// }
