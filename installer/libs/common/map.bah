//Internal function for hashing map keys. 
__Bah_map_hash(s cpstring, l int) int {
    hash = 5381

    i=0; for i < strlen(s), i++ {
        hash = (<int>noCheck(hash << 5) + 0) + hash + <int>s[i]
    }

    if hash < 0 {
        hash = 0 - hash
    }

    return hash % l
}

//Internal representation of an element inside a map.
struct mapElem {
    key: cpstring
    val: ptr
}


//An internal wrapper that is used for every map:* type.
struct mapWrapper {
    elems: []mapElem*
    length: int

    //Used for growing the map.
    grow(nb int) {
        l = len(this.elems)
        i=l; for i < l + nb, i++ {
            this.elems[i] = null
        }
        //hash of existing elems may have changed
        //try to re-locate elems if it is the case
        i=0; for i < l, i++ {
            e = this.elems[i]
            if e == null {
                continue
            }
            ind = __Bah_map_hash(e.key, len(this.elems))
            if ind != i {
                j=ind; for j < len(this.elems), j++ {
                    if this.elems[j] == null {
                        this.elems[i] = null
                        this.elems[j] = e
                        break
                    }
                }
            }
        }
    }

    //Used for setting an key to a pointer.
    set(k cpstring, e ptr) {   
        if this.length == 0 {
            this.grow(50)
        }
        if this.length * 2 >= len(this.elems) {
            this.grow(this.length)
        }
        elem = new mapElem{
            key: k
            val: e
        }
        ind = __Bah_map_hash(k, len(this.elems))
        i=0; for i < len(this.elems), i++ {
            j = (i + ind) % len(this.elems)
            ce = this.elems[j]
            if ce == null {
                this.elems[j] = elem
                this.length++
                break
            } else if ce.key == k {
                this.elems[j] = elem
                break
            } 
        }
    }

    //Used for setting a key to any data-type.
    //The data will be copied. 
    setAny(k cpstring, e ptr, s int) {
        if this.length == 0 {
            this.grow(50)
        }
        if this.length * 2 >= len(this.elems) {
            this.grow(this.length)
        }
        p = memoryAlloc(s)
        memcpy(p, e, s)
        elem = new mapElem{
            key: k
            val: p
        }
        ind = __Bah_map_hash(k, len(this.elems))
        i=0; for i < len(this.elems), i++ {
            j = (i + ind) % len(this.elems)
            ce = this.elems[j]
            if ce == null {
                this.elems[j] = elem
                this.length++
                break
            } else if ce.key == k {
                this.elems[j] = elem
                break
            } 
        }
    }

    //Used for deleting an element.
    delete(k cpstring) {
        ind = __Bah_map_hash(k, len(this.elems))
        i=0; for i < len(this.elems), i++ {
            j = (i + ind) % len(this.elems)
            e = this.elems[j]
            if e != null && e.key == k {
                this.elems[j] = null
            }
        }
    }

    //Used for getting a pointer to an element.
    get(k cpstring) ptr {
        ind = __Bah_map_hash(k, len(this.elems))
        i=0; for i < len(this.elems), i++ {
            j = (i + ind) % len(this.elems)
            e = this.elems[j]
            if e != null && e.key == k {
                return e.val
            }
        }
        return null
    }

}

//Used for generating a mapWrapper.
//This is an internal function called with the map keyword.
mapWrapper() mapWrapper* {
    m = new mapWrapper
    m.grow(50)
    return m
}