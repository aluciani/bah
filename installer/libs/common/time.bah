#include "string.bah"
#include "memory.bah"
#include "<time.h>"

const TIME_MS  = 1000000
const TIME_S   = 1000000000
const TIME_MIN = 60000000000

//This is a wrapper that makes time manipulations easier.
//By default, its .timestamp field is set to the current time.
struct time {
    timestamp: int

    //To set the timestamp to the current time.
    now() {
        this.timestamp = time(0)
    }
    
    //To format the timestamp using the specified format.
    //The format options can be found [here](https://koor.fr/C/ctime/strftime.wp).
    format(a cpstring) cpstring {
        
        lt int = localtime(&this.timestamp)

        date cpstring = memoryAlloc(1024)
        strftime(date, 1024, a, lt)
        return date
    } 

    //To get the number of seconds since the timestamp.
    since() int {
        nt = time(0)
        r = nt - this.timestamp
        return r
    }
}

struct! timespec {
    tv_sec: int
    tv_nsec: int
}

#linux {
    #define! timespec_get(ts timespec*, mode int)
}

#darwin {
    #define! clock_gettime(w int, t timespec*) int
}

#windows {
    #define! clock_gettime(w int, t timespec*) int
}


//Returns the current timestamp in nanoseconds.
//One nano second looks like this: 0000000000000000001.
getTimeUnix() int {
    ts = timespec{}
    #linux {
        timespec_get(&ts, noCheck(TIME_UTC))
    }
    #darwin {
        s = clock_gettime(noCheck(CLOCK_REALTIME), &ts)
    }
    #windows {
        s = clock_gettime(noCheck(CLOCK_REALTIME), &ts)
    }
    s = ts.tv_sec * 1000000000 + ts.tv_nsec
    return s
}

//Example
// #include "iostream.bah"
// #include "time.bah"
// main() {
// ts = time{}
// ts.now()
// formattedTime = ts.format("hou:min:sec")
// println(formattedTime)
// }
