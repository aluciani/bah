//These functions are used to generate all kinds of 'user-friendly' messages to the terminal.

#include "./lexer.bah"
#include "./logger.bah"

//ASCII COLORS
const ASCII_RESET = "\033[0m"

const ASCII_YELLOW = "\033[33m"
const ASCII_BLUE = "\033[36m"
const ASCII_GREEN = "\033[32m"
const ASCII_MAGENTA = "\033[35m"
const ASCII_GREY = "\033[2m"

#define advertiseGeneric() //defined in generics.bah

//Generates a function definition to be printed as more context for an error.
getFuncDef(fn func*) str {
    def = ASCII_YELLOW + fn.name + ASCII_RESET + "("
    i=0; for i < len(fn.args),i++ {
        def += ASCII_MAGENTA + fn.args[i].name + " " + ASCII_BLUE + fn.args[i].type + ASCII_RESET
        if i+1 < len(fn.args) {
            def += ", "
        }
    }

    def += ") "+fn.returns.type + " " + ASCII_GREEN + fn.file+":"+intToStr(fn.line) + ASCII_RESET
    return def
}

//Prints a formatted error to the terminal/adds an entry in '-debug' mode.
//It then exits the program.
//This function is somewhat huge for its purpose because it will find the token position inside the source file and print the whole line.
throwErr(tp ptr, format str) {
    str = ""
    info = ""
    pos = 0
    if tp != null {
        fs = fileStream{}
        fs.open(compilerState.currentFile, "r")
        fileSrcStr = fs.readContent()
        t Tok* = tp
        ffmt = string(format)
        if len(t.ogCont) == 0 {
            t.ogCont = t.cont
        }
        ffmt.replace("{TOKEN}", "'"+t.ogCont+"'")
        format = ffmt.str()

        line = []char

        beg=t.pos; for beg > 0 {
            c = fileSrcStr[beg]
            if c == <char>10 {
                beg++
                break
            }
            beg--
        }

        pos = t.pos - beg + 1
        size = fs.getSize()

        i=beg; for i < size, i++ {
            c = fileSrcStr[i]
            if c == <char>10 {
                break
            }
            if i == t.pos {
                errCont = []char
                offset = i
                for i < size, i++ {
                    if i-offset >= len(t.ogCont) {
                        break
                    }
                    if fileSrcStr[i] != t.ogCont[i-offset] {
                        if isSpace(fileSrcStr[i]) == false {
                            break
                        }
                        offset++
                    }
                    errCont[len(errCont)] = fileSrcStr[i]
                }
                errCont[len(errCont)] = null
                errTk = strToArr("\e[1;31m"+arrToStr(errCont)+"\e[1;37m")
                ii=0; for ii < len(errTk), ii++ {
                    line[len(line)] = errTk[ii]
                }
                if len(errCont) > 1 {
                    i--
                    continue
                }
            }
            line[len(line)] = c
        }

        fs.close()
        str = arrToStr(line)
        info = intToStr(t.line) + ":" + intToStr(pos)
    }

    if debug {
        t Tok* = tp
        format = noASCIIescape(format)
        debugError("error", t.line, pos, pos+len(t.ogCont), &format)
        debugExit()
    } else {
        advertiseGeneric()
        println("\e[1;31m[ERROR]\e[0m "+compilerState.currentFile+":" + info + "\n\e[1;37m\t" + str + "\e[0m\n" + format)
    }

    exit(1)

}

//Prints a warning to the terminal/adds an entry in '-debug' mode.
//This will not terminate the program.
throwWarning(s str) {
    if debug {
        s = noASCIIescape(s)
        debugError("warning", -1, -1, -1, &s)
        return
    }
    println("\e[1;33m[WARNING]\e[0m " + compilerState.currentFile + "\n\e[0m\t" + s + "\e[0m")
}

//Same as 'throwWarning()' but will also show the line number of the warning.
//This is the function called when using '#warning'.
throwWarningLine(s str, l int) {
    if debug {
        s = noASCIIescape(s)
        debugError("warning", l, 0, 0, &s)
        return
    }
    println("\e[1;33m[WARNING]\e[0m " + compilerState.currentFile + ":"+intToStr(l)+"\n\e[0m\t" + s + "\e[0m")
}

//Used for printinh a notice to the terminal/adding an entry in '-debug' mode.
//This will not terminate the program.
throwNoticeLine(s str, l int) {
    if debug {
        s = noASCIIescape(s)
        debugError("notice", l, 0, 0, &s)
        return
    }
    if noticeEnabled == false {
        return
    }
    println("\e[1;36m[NOTICE]\e[0m " + compilerState.currentFile + ":"+intToStr(l)+"\n\e[0m\t" + s + "\e[0m")
}