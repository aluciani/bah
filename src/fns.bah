#include "./globals.bah"

#define searchVar(name cpstring, t Tok*, elems Elems*) variable*

//This is stupid: are we in the global scope or inside a function.
isGlobal() bool {
    return currentFn == null
}

//Searches a function by name.
//This may return a new instance of func.
//This will also search for variable with func type and struct methods.
searchFunc(name cpstring, elems Elems*, inclCurr bool) func* {
    ogName = name
    if name == "noCheck" {
        fn = new func
        fn.name = "noCheck"
        return fn
    }

    n = string(name)
    fns = elems.fns
    i=0; for i < len(fns) {
        fn = fns[i]
        if fn.name == name {
            fn.used = true
            return fn
        }
        i = i + 1
    }

    if inclCurr == true && currentFn != null {
        if currentFn.name == name {
            currentFn.used = true
            return currentFn
        }
    }

    v = searchVar(ogName, null, elems)
    if v == null {
        return null
    }
    nf = new func
    nf.name = ogName
    cvt = string(v.type)
    if cvt.hasPrefix("function(") == 0 {
        return null
    }
    pfn = parseFnType(cvt)
    nf.returns = pfn.returns
    nf.isVar = true
    nf.args = pfn.args
    return nf
}

//This may be a faster yet let predictable way of searching for a function given a token.
//See searchVirtVarByToken() for more explaination.
searchFuncByToken(t Tok*, elems Elems*) func* {
    if t.getRefFn() != null {
        return t.getRefFn()
    }
    fnName = splitStringBefore(string(t.cont), "(")
    return searchFunc(fnName, elems, true)
}

searchVirtFuncByToken(t Tok*, elems Elems*) func* {
    let fn = <func*>t.getRefFn() then {
        return fn
    }

    let v = <variable*>t.getRefVar() then {
        if string(v.type).hasPrefix("function(") {
            fn = parseFnType(string(v.type))
            fn.name = v.name
            return fn
        }
    }
    let fn = funcCache[t.cont] then {
        return fn
    }

    fn = searchFunc(t.cont, elems, true)
    if fn != null && fn.isVar == false {
        funcCache[t.cont] = fn
    }
    return fn
}