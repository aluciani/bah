//These are all function used to generate the '-debug' output.
//Useful for making language servers, IDEs or just analyzing a program.
//The output can be HUGE.

#include "iostream.bah"

//All the debug struct wrapper, used to not output every information about the symbols.

struct debugFunctionArgument {
    name: cpstring
    type: cpstring
}

struct debugVar {
    name: cpstring
    type: cpstring
    isGlobal: bool
    isConst: bool
    
    fromVar(v variable*) {
        this.name = v.name
        this.type = v.type
        this.isGlobal = v.isGlobal
        this.isConst = v.isConst
    }
    
}

struct debugFunction {
    name: cpstring
    returns: cpstring
    args: []debugVar*
}

struct debugStruct {
    name: cpstring
    membs: []debugVar*
    methods: []cpstring
}


struct debugType {
    name: cpstring
    refers: cpstring
}

//Starts the debugging output.
debugStart() {
    println("[")
}

//Used for removing ASCII formating generated by functions of 'errors.bah'.
noASCIIescape(s cpstring) cpstring {
    sb = strBuilder{}

    i=0; for i < len(s), i++ {
        if s[i] == <char>27 {
            for i < len(s), i++ {
                if s[i] == 'm' {
                    break
                }
            }
            continue
        }
        sb.append(s[i])
    }

    return sb.str()

}

//Used for adding a debug entry.
debugPrint(name cpstring, line int, e reflectElement) {
    if debug == false {
        return
    }
    println("
    {
        \"name\": "+toJson(&name)+",
        \"path\": \""+compilerState.currentFile+":" + intToStr(line)+"\",
        \"element\": "+toJson(e)+"
    },
    ")    
}

//Used for adding a debug error entry.
//This is the same as debugPrint but with an addition range.
debugError(name cpstring, line int, from int, to int, e reflectElement) {
    if debug == false {
        return
    }
    println("
    {
        \"name\": "+toJson(&name)+",
        \"path\": \""+compilerState.currentFile+":" + intToStr(line)+"\",
        \"range\": [
            "+intToStr(from)+",
            "+intToStr(to)+"
        ],
        \"element\": "+toJson(e)+"
    },
    ")
}

//Used for ending the debugging output.
debugEnd() {
    println("
    {
        \"name\": \"file_end\"
    }
    ]
    ")
}


//Used for not expected ending the debugging output.
debugExit() {
    println("
    {
        \"name\": \"error_end\"
    }
    ]
    ")
}

//Used for adding var_end entries at the end of scopes.
debugEndScope(line int, elems Elems*) {
    if debug == false {
        return
    }
    i=0; for i < len(elems.vars) {
        v = elems.vars[i]
        if v.declScope == elems && v.isConst == false {
            name = v.name
            debugPrint("var_end", line, &name)
        }
        i = i + 1
    }
}
